plugins {
    id 'groovy'
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.16.1'
}

group = 'com.rundeck'
version = scmVersion.version

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

//git hubのタグと連動
scmVersion {
    useHighestVersion = true
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation{
        extendsFrom pluginLibs
    }
}

dependencies {
    implementation libs.rundeckCore
    pluginLibs(libs.jsch)
    implementation libs.gson

    testImplementation libs.bundles.testLibs
}

// task to copy plugin libs to output/lib dir
tasks.register('copyToLib', Copy) {
    into layout.buildDirectory.dir("output/lib")
    from configurations.pluginLibs
}

tasks.named('jar', Jar).configure {
    dependsOn tasks.named('copyToLib')

    archiveBaseName.set("sequential-commands-plugin")
    archiveVersion.set(project.version)

    from(sourceSets.main.output) // .class ファイルを含める
    from(layout.buildDirectory.dir("output")) // lib/ を含める

    manifest {
        def libList = configurations.pluginLibs.collect { "lib/${it.name}" }.join(' ')
        attributes(
                'Rundeck-Plugin-Classnames': pluginClassNames,
                'Rundeck-Plugin-Name': 'sequential-commands',
                'Rundeck-Plugin-Description': 'Provide a short description of your plugin here.',
                'Rundeck-Plugin-Rundeck-Compatibility-Version': '5.x',
                'Rundeck-Plugin-Tags': 'java,NodeStep',
                'Rundeck-Plugin-License': 'Apache 2.0',
                'Rundeck-Plugin-Source-Link': 'Please put the link to your source repo here',
                'Rundeck-Plugin-Target-Host-Compatibility': 'all',
                'Rundeck-Plugin-File-Version': project.version,
                'Rundeck-Plugin-Version': rundeckPluginVersion,
                'Rundeck-Plugin-Archive': 'true',
                'Rundeck-Plugin-Libs': libList
        )
    }
}

wrapper {
    gradleVersion = '7.1.1'
}
